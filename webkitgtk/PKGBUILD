# $Id: PKGBUILD 195213 2013-09-27 21:30:41Z heftig $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=webkitgtk
pkgver=2.2.1
pkgrel=1
pkgdesc="GTK+ Web content engine library"
arch=(i686 x86_64)
url="http://webkitgtk.org/"
license=(custom)
depends=(libxt libxslt sqlite libsoup libgl gtk3 gst-plugins-base-libs
         libsecret libwebp harfbuzz-icu)
makedepends=(gtk2 gperf gobject-introspection python mesa ruby gtk-doc)
optdepends=('gtk2: Netscape plugin support')
provides=("webkitgtk3=${pkgver}" "libwebkit3=${pkgver}")
conflicts=(webkitgtk3 libwebkit3)
replaces=(webkitgtk3 libwebkit3)
options=(!libtool !emptydirs)
source=(http://webkitgtk.org/releases/$pkgname-${pkgver}.tar.xz)
sha256sums=('13d081df8dd0a442d4f4e91a6d5236aa91b78f62a670283da08ad5bff76e4ae8')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/$pkgname \
    --enable-introspection --disable-geolocation --disable-spellcheck

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make all stamp-po
}

package() {
  cd $pkgname-$pkgver
  make -j1 DESTDIR="$pkgdir" install
  install -Dm644 Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
