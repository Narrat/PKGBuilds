# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Rich Lane <rlane@club.cc.cmu.edu>

_pkgname=oort
pkgname=oort-git
pkgver=r394.7aacb6d
pkgrel=1
pkgdesc="space fleet programming game"
arch=('i686' 'x86_64')
url="https://github.com/rlane/Oort"
license=("GPL")
depends=('glib2' 'gtk2' 'gtkglext' 'glew')
makedepends=(automake-1.11)
source=($_pkgname::git://github.com/rlane/Oort)
md5sums=('SKIP')


pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $_pkgname

  git submodule init
  git submodule update
}

build() {
  cd "$srcdir/$_pkgname"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_pkgname"

  make DESTDIR="$pkgdir" install
}
