# $Id: PKGBUILD 183157 2013-04-18 00:18:48Z heftig $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=webkitgtk
pkgname=('webkitgtk3')
pkgver=2.0.1
pkgrel=1
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
makedepends=('libxt' 'libxslt' 'sqlite' 'libsoup' 'libgl' 'gtk2' 'gtk3' 'gst-plugins-base-libs' 'gperf' 'gobject-introspection' 'python' 'mesa' 'ruby' 'gtk-doc' 'libsecret' 'libwebp')
options=('!libtool' '!emptydirs')
source=(http://webkitgtk.org/releases/$pkgbase-$pkgver.tar.xz)
sha256sums=('6303552a39d7912725d75bc55f530cdb99171fb873e299798d0bb932d6ea76cb')

prepare() {
  cd $pkgbase-$pkgver
  mkdir build-gtk3
}

build() {
  cd $pkgbase-$pkgver/build-gtk3

  # Suppress a *lot* of gcc 4.8 warnings
  export CPPFLAGS="$CPPFLAGS -Wno-unused-local-typedefs"

  ../configure --prefix=/usr --libexecdir=/usr/lib/webkitgtk3 \
    --enable-introspection --disable-geolocation --disable-spellcheck
  make all stamp-po
}

package_webkitgtk3() {
  pkgdesc="GTK+ Web content engine library for GTK+ 3.0"
  depends=('libxt' 'libxslt' 'sqlite' 'libsoup' 'libgl' 'gtk3' 'gst-plugins-base-libs' 'libsecret' 'libwebp')
  conflicts=('libwebkit3')
  provides=("libwebkit3=${pkgver}")
  replaces=('libwebkit3')
  optdepends=('gtk2: Webkit2 plugin support')
  
  cd $pkgbase-$pkgver/build-gtk3
  make -j1 DESTDIR="$pkgdir" install
  install -Dm644 ../Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
